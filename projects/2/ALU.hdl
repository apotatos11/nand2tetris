// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x, out=notx);
    Not16(in=y, out=noty);
    And16(a=x, b=notx, out=zerox);
    And16(a=y, b=noty, out=zeroy);

    Mux16(a=x, b=zerox, sel=zx, out=afterZeroX);
    Mux16(a=y, b=zeroy, sel=zy, out=afterZeroY);

    Not16(in=afterZeroX, out=notafterZeroX);
    Not16(in=afterZeroY, out=notafterZeroY);

    Mux16(a=afterZeroX, b=notafterZeroX, sel=nx, out=afterNotX);
    Mux16(a=afterZeroY, b=notafterZeroY, sel=ny, out=afterNoty);

    And16(a=afterNotX, b=afterNoty, out=afterZnAndXy);
    Add16(a=afterNotX, b=afterNoty, out=afterZnAddXy);
    Mux16(a=afterZnAndXy, b=afterZnAddXy, sel=f, out=afterF);

    Not16(in=afterF, out=afterNotF);
    Mux16(a=afterF, b=afterNotF, sel=no, out=out, out[15]=ng, out[0..7]=lowBit, out[8..15]=highBit);

    Or8Way(in=lowBit, out=resultLowBit);
    Or8Way(in=highBit, out=resultHighBit);
    Or(a=resultLowBit, b=resultHighBit, out=resultNotZr);
    Not(in=resultNotZr, out=zr);
}